// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=registerValue, out=Inc16registerValue);
    Mux16(a=registerValue, b=Inc16registerValue, sel=inc, out=Mux16Inc16registerValue);
    Mux16(a=Mux16Inc16registerValue, b=in, sel=load, out=Mux16load);
    Mux16(a=Mux16load, b=false, sel=reset, out=Mux16reset);
    Or(a=inc, b=load, out=incOrload);
    Or(a=incOrload, b=reset, out=incOrloadOrreset);
    Register(in=Mux16reset, load=incOrloadOrreset, out=registerValue, out=out);
}
